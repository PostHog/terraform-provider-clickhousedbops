resource "clickhousedbops_role" "reader" {
  cluster_name = var.cluster_name
  name         = "reader"
}

resource "clickhousedbops_user" "john" {
  cluster_name                    = var.cluster_name
  name                            = "john"
  password_sha256_hash_wo         = sha256("test")
  password_sha256_hash_wo_version = 1
}

resource "clickhousedbops_grant_privilege" "grant_show_to_role" {
  cluster_name      = var.cluster_name
  privilege_name    = "SHOW"
  database_name     = "default"
  grantee_role_name = clickhousedbops_role.reader.name
  grant_option      = false
}

resource "clickhousedbops_grant_privilege" "grant_dictget_to_role" {
  cluster_name      = var.cluster_name
  privilege_name    = "dictGet"
  database_name     = "default"
  grantee_role_name = clickhousedbops_role.reader.name
  grant_option      = false
}

resource "clickhousedbops_grant_privilege" "grant_insert_on_table_to_user" {
  cluster_name      = var.cluster_name
  privilege_name    = "INSERT"
  database_name     = "default"
  table_name        = "tbl1"
  grantee_user_name = clickhousedbops_user.john.name
  grant_option      = true
}

resource "clickhousedbops_grant_privilege" "grant_select_on_single_column_on_table_to_user" {
  cluster_name      = var.cluster_name
  privilege_name    = "SELECT"
  database_name     = "default"
  table_name        = "tbl1"
  column_name       = "count"
  grantee_user_name = clickhousedbops_user.john.name
  grant_option      = true
}

resource "clickhousedbops_grant_privilege" "grant_sources1" {
  for_each          = toset(["URL", "S3", "REMOTE"])
  cluster_name      = var.cluster_name
  privilege_name    = each.key
  grantee_user_name = clickhousedbops_user.john.name
  grant_option      = true
}

resource "clickhousedbops_grant_privilege" "grant_sources2" {
  for_each          = toset(["KAFKA", "POSTGRES"])
  cluster_name      = var.cluster_name
  privilege_name    = each.key
  grantee_user_name = clickhousedbops_user.john.name
  grant_option      = false
}

resource "clickhousedbops_grant_privilege" "grant_current_grants_to_role" {
  count = (var.cluster_name == null) ? 1 : 0

  cluster_name      = var.cluster_name
  privilege_name    = "SELECT"
  grantee_role_name = clickhousedbops_role.reader.name
  current_grants    = true
}

resource "clickhousedbops_grant_privilege" "grant_user_admin_to_role" {
  for_each          = toset(["CREATE USER", "DROP USER", "ALTER USER", "CREATE ROLE"])
  cluster_name      = var.cluster_name
  privilege_name    = each.key
  grantee_role_name = clickhousedbops_role.reader.name
  grant_option      = true
}

resource "clickhousedbops_grant_privilege" "grant_create_user_on_object_to_user" {
  cluster_name      = var.cluster_name
  privilege_name    = "CREATE USER"
  access_object     = "team_*"
  grantee_user_name = clickhousedbops_user.john.name
  grant_option      = true
}
